/*
 * Copyright 2015 Sean Brandt
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven { url "https://repo.spring.io/plugins-release" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.2")
        classpath("io.spring.gradle:docbook-reference-plugin:0.3.1")
        classpath("ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.9.1")
        classpath 'org.scoverage:gradle-scoverage:1.0.9'
    }
}

configure(allprojects) {
    group 'net.muxserver.krakenmush'
    version '1.0-SNAPSHOT'


    ext.scalaBaseVersion = '2.11'
    ext.scalaVersion = "${scalaBaseVersion}.7"
    ext.slf4jVersion = '1.7.12'
    ext.logbackVersion = '1.1.3'
    ext.akkaVersion = '2.4.0-RC2'
    ext.scoverageVersion = '1.1.0'
    ext.springVersion = '4.2.1.RELEASE'
    ext.reactiveMongoVersion = '0.11.7'

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'scoverage'

    compileJava.options*.compilerArgs = [
            "-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:try", "-Xlint:fallthrough", "-Xlint:rawtypes", "-Xlint:deprecation",
            "-Xlint:unchecked", "-Xlint:-options", "-Werror"
    ]

    compileTestJava.options*.compilerArgs = [
            "-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:try", "-Xlint:-fallthrough", "-Xlint:-rawtypes", "-Xlint:-deprecation",
            "-Xlint:-unchecked", "-Xlint:-options"]

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.compilerArgs += "-parameters"
    }

    compileScala {
        scalaCompileOptions.useCompileDaemon = true
        scalaCompileOptions.additionalParameters = [
                "-deprecation",
                "-explaintypes",
                "-feature",
                "-language:postfixOps"
        ]

    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.useAnt = false
    }


    test {
        systemProperty("java.awt.headless", "true")
        systemProperty("testGroups", project.properties.get("testGroups"))
        scanForTestClasses = false
        include(["**/*Tests.class", "**/*Test.class"])
        // Since we set scanForTestClasses to false, we need to filter out inner
        // classes with the "$" pattern; otherwise, using -Dtest.single=MyTests to
        // run MyTests by itself will fail if MyTests contains any inner classes.
        exclude(["**/Abstract*.class", '**/*$*'])
    }

    repositories {
        jcenter()
        maven {
            name = "typesafe-maven-release"
            url = "https://repo.typesafe.com/typesafe/maven-releases"
        }
        mavenCentral()
        maven {
            name = "sonatype-oss-releases"
            url = "http://oss.sonatype.org/content/repositories/releases/"
        }
        maven {
            name = "sonatype-oss-snapshots"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    sourceSets.all { ext.purpose = null }

    sourceSets {
        main {
            purpose = "production"
        }
        test {
            purpose = "test"
        }
    }

    //noinspection GroovyAssignabilityCheck
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'commons-logging') {
                details.useTarget "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
            }
            if (details.requested.name == 'log4j') {
                details.useTarget "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
            }
            if (details.requested.group == "org.apache.logging.log4j") {
                details.useTarget 'org.apache.logging.log4j:log4j-to-slf4j:2.3'
            }
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        compile "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
        compile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
        compile "org.slf4j:slf4j-ext:${slf4jVersion}"
        compile "org.scala-lang:scala-library:${scalaVersion}"
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "com.typesafe.scala-logging:scala-logging_${scalaBaseVersion}:3.1.0"
        compile "com.typesafe:config:1.3.0"
        compile 'com.google.inject:guice:4.0'
        compile "net.codingwell:scala-guice_${scalaBaseVersion}:4.0.0"
        compile "org.scalaz:scalaz-core_${scalaBaseVersion}:7.1.3"
        compile "net.ceedubs:ficus_${scalaBaseVersion}:1.1.2"
        compile 'com.google.code.findbugs:annotations:3.0.0'
        compile "codes.reactive:scala-time_${scalaBaseVersion}:0.3.0-SNAPSHOT"
        compile "org.json4s:json4s-native_${scalaBaseVersion}:3.2.11"
        compile "org.reactivemongo:reactivemongo_${scalaBaseVersion}:${reactiveMongoVersion}"
        testCompile "junit:junit:4.11"
        testCompile "org.scalatest:scalatest_${scalaBaseVersion}:2.2.5"
        testCompile "org.mockito:mockito-all:1.9.5"
        scoverage "org.scoverage:scalac-scoverage-plugin_${scalaBaseVersion}:${scoverageVersion}",
                "org.scoverage:scalac-scoverage-runtime_${scalaBaseVersion}:${scoverageVersion}"
    }
}

configure(rootProject) {

    apply plugin: 'idea'

    dependencies {
        compile "com.typesafe.akka:akka-actor_${scalaBaseVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-remote_${scalaBaseVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster_${scalaBaseVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-metrics_${scalaBaseVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-tools_${scalaBaseVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-slf4j_${scalaBaseVersion}:${akkaVersion}"
        compile 'io.kamon:sigar-loader:1.6.6-rev002'
        testCompile "com.typesafe.akka:akka-testkit_${scalaBaseVersion}:${akkaVersion}"

    }
}




